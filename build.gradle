buildscript {
    ext.kotlin_version = '1.2.61'
    ext.sponge_version = "7.1.0-SNAPSHOT"
    ext.Coroutines_version = "0.23.4"

    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven { url = "http://files.minecraftforge.net/maven" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}


plugins {
    id 'java'
    id "idea"
    id "signing"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'kotlin'

group 'one.oktw'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'
    compileOnly 'com.github.CrushedPixel:PacketGate:33902a1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


minecraft {
    version = "1.12.2-14.23.4.2736"
    mappings = "snapshot_20180813"
    makeObfSourceJar = false
}

shadowJar {
    classifier = null
    exclude "dummyThing"
    exclude "META-INF/*.kotlin_module"
    exclude "META-INF/fml_*"

    dependencies {
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "kotlin", "me.mmis10000.relocate.kotlin"
    relocate "kotlinx", "me.mmis10000.relocate.kotlinx"
}

signing {
    required {signing.hasProperty('keyId')}
    sign configurations.archives
}


jar {
    manifest.attributes(
            "FMLAT": "i18n_at.cfg"
    )
}

jar.dependsOn(shadowJar)
jar.enabled = false