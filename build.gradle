buildscript {
    ext.kotlin_version = '1.2.61'
    ext.sponge_version = "7.1.0-SNAPSHOT"
    ext.Coroutines_version = "0.23.4"

    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven { url = "http://files.minecraftforge.net/maven" }
    }

    dependencies {
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}


plugins {
    id 'java'
    id "idea"
    id "signing"
    id "org.spongepowered.plugin" version "0.8.1"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'kotlin'

group 'one.oktw'
version '0.1.4'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'org.spongepowered:spongeapi:7.1.0'
    compile 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    compile 'org.spongepowered:spongecommon:7.1.0:dev'
    compile 'com.github.CrushedPixel:PacketGate:33902a1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

extractAnnotationsJar.enabled = false

minecraft {
    version = "1.12.2-14.23.4.2736"
    mappings = "snapshot_20180808"
    makeObfSourceJar = false
}

shadowJar {
    classifier = null
    exclude "dummyThing"
    exclude "META-INF/fml_*"

    dependencies {
        include dependency("org.jetbrains.kotlin:")
        include dependency("org.jetbrains.kotlinx:")
    }

    relocate "kotlin", "one.oktw.i18n.relocate.kotlin"
    relocate "kotlinx", "one.oktw.i18n.relocate.kotlinx"
}

signing {
    required {signing.hasProperty('keyId')}
    sign configurations.archives
}

sourceSets {
    main {
        ext.refMap = "mixins.i18n.refmap.json"
    }
}

jar {
    manifest.attributes(
            "FMLAT": "i18n_at.cfg",
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.i18n.json',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod' : true
    )
}

task packageSources(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task packageAPI(type: Jar, dependsOn: 'shadowJar') {
    from(sourceSets.main.output) {
        include "one/oktw/i18n/api/**"
    }

    classifier = 'api'
}

artifacts {
    archives packageSources
    archives packageAPI
}

jar.dependsOn(shadowJar)
jar.enabled = false